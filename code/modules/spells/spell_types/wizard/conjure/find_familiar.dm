/mob/var/tmp/busy_summoning_familiar = FALSE

/obj/effect/proc_holder/spell/self/findfamiliar
	name = "Поиск фамильяра"
	desc = "Призывает верного магического спутника в помощь в приключениях. Повторное использование заклинания с активным фамильяром может пробудить его разум."
	overlay_state = "null"
	sound = list('sound/magic/whiteflame.ogg')
	active = FALSE

	recharge_time = 5 SECONDS
	chargetime = 2 SECONDS

	warnie = "spellwarning"

	chargedloop = /datum/looping_sound/invokegen
	associated_skill = /datum/skill/magic/arcane

	xp_gain = TRUE
	spell_tier = 1
	cost = 1

	invocation = "Appare, spiritus fidus."
	invocation_type = "whisper"

	var/mob/living/simple_animal/pet/familiar/fam
	var/familiars = list()

/obj/effect/proc_holder/spell/self/findfamiliar/Initialize()
	. = ..()
	familiars = GLOB.familiar_types.Copy()


/obj/effect/proc_holder/spell/self/findfamiliar/cast(list/targets, mob/living/carbon/user)
	if (!user)
		return FALSE

	// Prevent multiple simultaneous summon attempts
	if (user.busy_summoning_familiar)
		to_chat(user, span_warning("Вы уже пытаетесь призвать фамильяра! Дождитесь завершения текущего призыва."))
		return FALSE

	user.busy_summoning_familiar = TRUE

	. = ..()

	// Check if user already has a familiar
	for (var/mob/living/simple_animal/pet/familiar/fam in GLOB.alive_mob_list + GLOB.dead_mob_list)
		if (fam.familiar_summoner == user)
			if(fam.health <= 0)
				var/choice = input(user, "Ваш фамильяр мёртв. Что вы хотите сделать?") as null|anything in list("Оживить магическим камнем", "Освободить")
				if(choice == "Оживить магическим камнем")
					to_chat(user, span_notice("Для воскрешения вам понадобится магический камень в активной руке."))
					var/obj/item/natural/stone/magic_stone = user.get_active_held_item()
					if(!istype(magic_stone, /obj/item/natural/stone) || magic_stone.magic_power < 1)
						to_chat(user, span_warning("Вы должны держать магический камень в активной руке!"))
						user.busy_summoning_familiar = FALSE
						revert_cast()
						return FALSE
					else
						revive_familiar(magic_stone, fam, user)
						if (fam.buff_given)
							user.apply_status_effect(fam.buff_given)
						user.busy_summoning_familiar = FALSE
						return TRUE
				else if(choice == "Освободить")
					free_familiar(fam, user)
					if(!fam?.mind)
						log_game("[key_name(user)] has released their familiar: [fam.name] ([fam.type])")
					else
						log_game("[key_name(user)] released sentient familiar [key_name(fam)] ([fam.type])")
					user.busy_summoning_familiar = FALSE
					revert_cast()
					return FALSE
				else // Cancel
					user.busy_summoning_familiar = FALSE
					revert_cast()
					return FALSE
			else
				var/choice = input(user, "У вас уже есть фамильяр. Хотите освободить его?") as null|anything in list("Yes", "No")
				if (choice != "Yes")
					user.busy_summoning_familiar = FALSE
					revert_cast()
					return FALSE
				free_familiar(fam, user)
				if(!fam?.mind)
					log_game("[key_name(user)] has released their familiar: [fam.name] ([fam.type])")
				else
					log_game("[key_name(user)] released sentient familiar [key_name(fam)] ([fam.type])")
				user.busy_summoning_familiar = FALSE
				revert_cast()
				return FALSE

	// Check for valid spawn turf before spawning familiar
	var/turf/spawn_turf = get_step(user, user.dir)
	if (!isturf(spawn_turf) || !isopenturf(spawn_turf))
		to_chat(user, span_warning("Недостаточно места для призыва фамильяра."))
		revert_cast()
		user.busy_summoning_familiar = FALSE
		return FALSE

	// Ask how the user wants to summon
	var/path_choice = input(user, "Как вы хотите призвать фамильяра?") as null|anything in list(
		"Призвать зарегистрированного фамильяра",
		"Призвать неразумного фамильяра"
	)

	if (path_choice == "Призвать зарегистрированного фамильяра")
		var/list/candidates = list()

		// Build list of valid candidate clients
		for (var/client/c_client in GLOB.familiar_queue)
			var/datum/familiar_prefs/pref = c_client.prefs?.familiar_prefs
			if (pref && familiars)
				for (var/fam_type in familiars)
					if (ispath(pref.familiar_specie, familiars[fam_type]))
						candidates += c_client
						break

		if (!candidates.len)
			to_chat(user, span_notice("Нет доступных кандидатов в фамильяры."))
			user.busy_summoning_familiar = FALSE
			revert_cast()
			return FALSE

		while (TRUE)
			var/list/name_map = list()
			for (var/client/c_candidate in candidates)
				var/datum/familiar_prefs/pref = c_candidate.prefs?.familiar_prefs
				if (pref?.familiar_name)
					name_map[pref.familiar_name] = list("client" = c_candidate, "pref" = pref)

			var/choice = input(user, "Выберите зарегистрированного фамильяра для осмотра:") as null|anything in name_map
			if (!choice)
				user.busy_summoning_familiar = FALSE
				revert_cast()
				return FALSE

			var/entry = name_map[choice]
			var/client/target = entry["client"]
			var/datum/familiar_prefs/pref = entry["pref"]

			if (!pref)
				to_chat(user, span_warning("Этот фамильяр больше недоступен."))
				GLOB.familiar_queue -= target
				continue

			show_familiar_preview(user, pref)

			var/confirm = input(user, "Призвать этого фамильяра?") as null|anything in list("Yes", "No")
			if (confirm != "Yes")
				winset(user.client, "Familiar Inspect", "is-visible=false")
				continue

			// Check that target is valid
			if (!target || (!isobserver(target.mob) && !isnewplayer(target.mob)))
				to_chat(user, span_warning("Этот фамильяр больше недоступен."))
				user.busy_summoning_familiar = FALSE
				GLOB.familiar_queue -= target
				revert_cast()
				return FALSE

			switch(askuser(target.mob, "[user.real_name] пытается призвать вас в качестве фамильяра. Принимаете?", "Ответьте в течение [DisplayTimeText(200)]!", "Yes", "No", StealFocus=0, Timeout=200))
				if(1)
					to_chat(target.mob, span_notice("Вы — магический фамильяр [user.real_name], связанный контрактом помогать, но инстинкт самосохранения всё ещё при вас."))
					GLOB.familiar_queue -= target
					spawn_familiar_for_player(target.mob, user)
					log_game("[user.ckey] summoned [pref.familiar_name] ([pref.familiar_specie]) controlled by [target.ckey]")
					if(target && target.mob)
						winset(target.mob, "Be a Familiar", "is-visible=false")
					user.busy_summoning_familiar = FALSE
					return TRUE
				if(2)
					to_chat(target.mob, span_notice("Зарегистрирован ответ: Нет."))
					to_chat(user, span_notice("Фамильяр воспротивился вашему призыву."))
					user.busy_summoning_familiar = FALSE
					revert_cast()
					return FALSE
				else
					to_chat(user, span_notice("Фамильяр слишком долго не отвечал на призыв, магия исчерпана."))
					user.busy_summoning_familiar = FALSE
					revert_cast()
					return FALSE

	if(path_choice == "Призвать неразумного фамильяра")
		// Non-sentient familiar summoning
		var/familiarchoice = input("Выберите фамильяра", "Доступные фамильяры") as anything in familiars
		var/mob/living/simple_animal/pet/familiar/familiar_type = familiars[familiarchoice]
		var/mob/living/simple_animal/pet/familiar/fam = new familiar_type(spawn_turf)
		fam.familiar_summoner = user
		user.visible_message(span_notice("[fam.summoning_emote]"))
		fam.fully_replace_character_name(null, "[user.real_name]'s familiar")
		var/faction_to_add = "[user.mind.current.real_name]_faction" //Should stop necromancer's skellies from murdering the necromancer's pet.
		fam.faction |= faction_to_add
		log_game("[key_name(user)] summoned non-sentient familiar of type [familiar_type]")
		user.busy_summoning_familiar = FALSE
		if (fam.buff_given)
			user.apply_status_effect(fam.buff_given)
		return TRUE
	else
		user.busy_summoning_familiar = FALSE
		revert_cast()
		return FALSE

///Used to show a preview of the prospective familiar's inspect window to the summoner.
/proc/show_familiar_preview(mob/user, datum/familiar_prefs/pref)
	if (!pref)
		return

	var/list/dat = list()
	var/title = pref.familiar_name ? pref.familiar_name : "Unnamed Familiar"

	dat += "<div align='center'><font size=5 color='#dddddd'><b>[title]</b></font></div>"

	// Add species name below the title, centered
	var/specie_type = GLOB.familiar_display_names[pref.familiar_specie]
	dat += "<div align='center'><font size=4 color='#bbbbbb'>[specie_type]</font></div>"

	// Add pronouns below species name
	var/list/pronoun_display = list(
		HE_HIM = "he/him",
		SHE_HER = "she/her",
		THEY_THEM = "they/them",
		IT_ITS = "it/its"
	)
	var/selected_pronoun = pronoun_display[pref.familiar_pronouns] ? pronoun_display[pref.familiar_pronouns] : "they/them"
	dat += "<div align='center'><font size=3 color='#bbbbbb'>[selected_pronoun]</font></div>"

	if (valid_headshot_link(null, pref.familiar_headshot_link, TRUE))
		dat += "<div align='center'><img src='[pref.familiar_headshot_link]' width='325px' height='325px'></div>"

	if (pref.familiar_flavortext_display)
		dat += "<div align='left'>[pref.familiar_flavortext_display]</div>"

	if (pref.familiar_ooc_notes_display)
		dat += "<br><div align='center'><b>OOC notes</b></div>"
		dat += "<div align='left'>[pref.familiar_ooc_notes_display]</div>"

	if (pref.familiar_ooc_extra)
		dat += pref.familiar_ooc_extra

	var/datum/browser/popup = new(user, "Familiar Inspect", nwidth = 600, nheight = 800)
	popup.set_content(dat.Join("\n"))
	popup.open(FALSE)

///Used to free a familiar from its summoner.
/proc/free_familiar(mob/living/simple_animal/pet/familiar/fam, mob/living/carbon/user)
	if (QDELETED(fam))
		to_chat(user, span_warning("Фамильяр уже исчез."))
		return
	to_chat(user, span_warning("Вы чувствуете, как ваша связь с [fam.name] разрывается."))
	to_chat(fam, span_warning("Вы чувствуете, как ваша связь с [user.name] разрывается, вы свободны."))

	fam.familiar_summoner = null
	if (fam.buff_given)
		user.remove_status_effect(fam.buff_given)

	user.mind?.RemoveSpell(/obj/effect/proc_holder/spell/self/message_familiar)
	fam.mind?.RemoveSpell(/obj/effect/proc_holder/spell/self/message_summoner)

	if (!fam.mind)
		var/exit_msg
		if (isdead(fam))
			exit_msg = "Труп [fam.name] исчезает в клубах дыма."
		else
			exit_msg = "[fam.name] в последний раз смотрит в сторону [user.name], затем открывает портал и исчезает в нём."

		fam.visible_message(span_warning(exit_msg))
		qdel(fam)


/proc/spawn_familiar_for_player(mob/chosen_one, mob/living/carbon/user)
	if (!chosen_one || !user)
		return

	// Handle if the chosen one is a new player lobby mob
	if (isnewplayer(chosen_one))
		var/mob/dead/new_player/new_chosen = chosen_one
		new_chosen.close_spawn_windows()

	// Ensure the chosen one has a valid client and preferences
	var/client/client_ref = chosen_one.client
	if (!client_ref || !client_ref.prefs)
		to_chat(user, span_warning("Призыв фамильяра провален: цель не имеет настроек или недействительна."))
		return

	var/datum/familiar_prefs/prefs = client_ref.prefs.familiar_prefs
	if (!prefs || !prefs.familiar_specie)
		to_chat(user, span_warning("Призыв фамильяра провален: цель не имеет допустимой формы фамильяра."))
		return

	// Spawn the familiar mob near the summoner
	var/turf/spawn_turf = get_step(user, user.dir)
	var/mob/living/simple_animal/pet/familiar/awakener = new prefs.familiar_specie(spawn_turf)
	if (!awakener)
		to_chat(user, span_warning("Призыв фамильяра провален: не удалось создать фамильяра."))
		return

	// Set summoner and name
	awakener.familiar_summoner = user
	awakener.fully_replace_character_name(null, prefs.familiar_name)
	awakener.pronouns = prefs.familiar_pronouns

	// Display summoning emote
	user.visible_message(span_notice("[awakener.summoning_emote]"))

	// Apply summoner's familiar buff
	if (awakener.buff_given)
		user.apply_status_effect(awakener.buff_given)

	// Transfer player's mind to the familiar
	if (!chosen_one.mind)
		to_chat(user, span_warning("Призыв фамильяра провален: цель не обладает разумом для переноса."))
		qdel(awakener)
		return

	if (chosen_one.ckey)
		awakener.ckey = chosen_one.ckey

	var/datum/mind/mind_datum = awakener.mind
	if (!mind_datum)
		to_chat(user, span_warning("Призыв фамильяра провален: перенос разума не удался."))
		qdel(awakener)
		return

	// Clear existing spells, then grant familiar communication
	mind_datum.RemoveAllSpells()
	mind_datum.AddSpell(new /obj/effect/proc_holder/spell/self/message_summoner)
	user.mind?.AddSpell(new /obj/effect/proc_holder/spell/self/message_familiar)

	// Add familiar's inherent spells
	if (awakener.inherent_spell)
		for (var/spell_path in awakener.inherent_spell)
			if (ispath(spell_path))
				var/obj/effect/proc_holder/spell/spell_instance = new spell_path
				if (spell_instance)
					mind_datum.AddSpell(spell_instance)

	// Disable automated/AI features
	awakener.can_have_ai = FALSE
	awakener.AIStatus = AI_OFF
	awakener.stop_automated_movement = TRUE
	awakener.stop_automated_movement_when_pulled = TRUE
	awakener.wander = FALSE

	var/faction_to_add = "[user.mind.current.real_name]_faction" //Should stop necromancer's skellies from murdering the necromancer's pet.
	awakener.faction |= faction_to_add

	// Admin/game logging
	log_game("[key_name(user)] has summoned [key_name(chosen_one)] as familiar '[awakener.name]' ([awakener.type]).")

/obj/effect/proc_holder/spell/self/message_familiar
	name = "Сообщение фамильяру"
	desc = "Шепчет сообщение в голову вашего фамильяра."

/obj/effect/proc_holder/spell/self/message_familiar/cast(list/targets, mob/user)
	. = ..()
	var/mob/living/simple_animal/pet/familiar/familiar
	for(var/mob/living/simple_animal/pet/familiar/familiar_check in GLOB.player_list)
		if(familiar_check.familiar_summoner == user)
			familiar = familiar_check
	if(!familiar || !familiar.mind)
		revert_cast()
		to_chat(user, "Вы не чувствуете разума своего фамильяра.")
		return
	var/message = input(user, "Вы устанавливаете связь. Что вы хотите сказать?")
	if(!message)
		revert_cast()
		return
	to_chat_immediate(familiar, "Арканный шёпот наполняет ваш разум, превращаясь в голос [user]: <font color=#7246ff>[message]</font>")
	user.visible_message("[user] бормочет заклинание, и его рот ненадолго вспыхивает белым.")
	user.whisper(message)
	log_game("[key_name(user)] sent a message to [key_name(familiar)] with contents [message]")
	return TRUE

/proc/revive_familiar(obj/item/natural/stone/magic_stone, mob/living/simple_animal/pet/familiar/fam, mob/living/carbon/user)
    // Consume the stone
    to_chat(user, span_notice("Вы направляете магию камня в [fam.name], воскрешая его!"))
    qdel(magic_stone)

    // Revive and fully heal the familiar
    fam.revive(full_heal = TRUE, admin_revive = TRUE)
    fam.familiar_summoner = user
    fam.visible_message(span_notice("[fam.name] возвращён к жизни магией [user]!"))
    return TRUE
